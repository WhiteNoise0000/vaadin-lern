plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.4'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.vaadin' version '24.1.3'
	// ライセンスレポート出力用プラグイン
	id "com.github.jk1.dependency-license-report" version "2.1"
}

group = 'jp.whitenoise'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://maven.vaadin.com/vaadin-prereleases'
	}
	maven {
		url 'https://maven.vaadin.com/vaadin-addons'
	}
}

ext {
	set('vaadinVersion', "24.1.3")
}

// ビルド環境判定→ローカル以外は商用ビルド
def notLocal = !project.hasProperty('isLocal')
vaadin {
	productionMode = notLocal
}

dependencies {
	// フロント
	implementation 'com.vaadin:vaadin-core'
	implementation 'com.vaadin:vaadin-spring-boot-starter'
//	implementation 'com.wontlost:ckeditor-vaadin:2.3.4'
	implementation 'org.vaadin:tinymce-for-flow:3.2.0'
	implementation 'com.wontlost:zxing-vaadin:2.0.1'

	// バックエンド
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// DAO, DTO
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2'
	runtimeOnly 'com.h2database:h2'
//	runtimeOnly 'org.postgresql:postgresql'

	// 開発, テスト
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

jar {
	enabled = false
}

bootJar {
	// launchScript()
	archiveFileName = 'vaadin-lern.jar'
}

// Podmanでビルド
tasks.named("bootBuildImage") {
	docker {
		host = "unix:///run/user/1000/podman/podman.sock"
		bindHostToBuilder = true
	}
}